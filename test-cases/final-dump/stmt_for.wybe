pub def single_generator use !io {
    ?x = [1, 2, 3]

    for ?i in x {
        !println(i)
    }
}

pub def multiple_generator use !io {
    ?x = [1, 2, 3]
    ?y = [4, 5, 6]

    for ?i in x; ?j in y {
        !println(i)
        !println(j)
    }
}

pub def shortest_generator_termination use !io {
    ?x = [1, 2, 3, 4]
    ?y = [4, 5]

    for ?i in x
        ?j in y {
        !println(i)
        !println(j)
    }
}

pub def using_break use !io {
    ?x = [1, 2, 3, 4]

    for ?i in x {
        if {
            i = 3 :: break
        }
        !println(i)
    }
}

pub def using_next use !io {
    ?x = [1, 2, 3, 4]

    for ?i in x {
        if {
            i = 3 :: next
        }
        !println(i)
    }
}

pub def using_while use !io {
    ?x = [1, 2, 3, 4]

    for ?i in x {
        while i < 3
        !println(i)
    }
}

pub def using_until use !io {
    ?x = [1, 2, 3, 4]

    for ?i in x {
        until i = 3
        !println(i)
    }
}

pub def using_when use !io {
    ?x = [1, 2, 3, 4]

    for ?i in x {
        when i < 3
        !println(i)
    }
}

pub def using_unless use !io {
    ?x = [1, 2, 3, 4]

    for ?i in x {
        unless i < 3
        !println(i)
    }
}

pub type int_sequence {
    pub int_sequence(start: int, stride: int, end: int)

    pub def {test} `[|]`(?value: int, ?rest: int_sequence, current: int_sequence) {
        int_sequence(?s, ?st, ?en, current)

        if {
            st < 0 :: (s > en)
            | else      :: (s < en)
        }

        ?value = s
        ?rest = int_sequence(s + st, st, en)
    }
}

pub def xrange(start: int, stride: int, end: int): int_sequence =
    int_sequence(start, stride, end)

pub def irange(start: int, stride: int, end: int): int_sequence =
    int_sequence(start, stride, if {stride < 0:: end - 1 | else:: end+1})

pub def using_xrange use !io {
    for ?i in xrange(1, 1, 10):int_sequence {
        !println(i)
    }
}

pub def using_xrange_reverse use !io {
    for ?i in xrange(10, -1, 1):int_sequence {
        !println(i)
    }
}

pub def using_irange use !io {
    for ?i in irange(1, 1, 10):int_sequence {
        !println(i)
    }
}

pub def using_irange_reverse use !io {
    for ?i in irange(10, -1, 1):int_sequence {
        !println(i)
    }
}

pub def {test} semi_det_for_loop {
    for ?i in xrange(0, 1, 10):int_sequence { i < 5 }
}


# Testing code
!println("single_generator:")
!single_generator
!println("\nmultiple_generator")
!multiple_generator
!println("\nshortest_generator_termination")
!shortest_generator_termination
!println("\nusing_break")
!using_break
!println("\nusing_next")
!using_next
!println("\nusing_while")
!using_while
!println("\nusing_until")
!using_until
!println("\nusing_when")
!using_when
!println("\nusing_unless")
!using_unless
!println("\nusing_xrange")
!using_xrange
!println("\nusing_xrange_reverse")
!using_xrange_reverse
!println("\nusing_irange")
!using_irange
!println("\nusing_irange_reverse")
!using_irange_reverse
# !println("\nsemi_det_for_loop")
# !semi_det_for_loop
