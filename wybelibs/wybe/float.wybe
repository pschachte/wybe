# purpose: Standard Floating Point Library
# since  : 0.1

pragma no_standard_library  # Standard library can't depend on itself!

use wybe.bool, wybe.int, wybe.count
use wybe.control, wybe.comparison, wybe.io, wybe.string, wybe.char

use foreign library m  # for math functions

representation is 64 bit float

## Constants

# The ratio of a circle's circumference to its diameter.
pub def pi:_ = 3.14159265358979323846264338327950288

# The base of natural logarithms.  e = exp(1.0).
pub def e:_ = 2.71828182845904523536028747135266250

# The largest finite float; the smallest is its negative
def magnitude_maximum = 1.7976931348623157e308


## Common floating point operations

# Addition.  This operation is reversible:  you can compute either argument from
# the other argument and the value.
pub def foreign llvm `+`=fadd(x:_, y:_):_
pub def (?x:_ + y:_):_  = ?z where { ?x = foreign llvm fsub(z,y) }
pub def (x:_  + ?y:_):_ = ?z where { ?y = foreign llvm fsub(z,x) }

# Addition assignment.
pub def (!x:_ += y:_) { ?x = x + y }

# Subtraction.  This operation is reversible:  you can compute either argument
# from the other argument and the value.
pub def foreign llvm `-`=fsub(x:_, y:_):_
pub def (?x:_ - y:_):_  = ?z where { ?x = foreign llvm fadd(z,y) }
pub def (x:_  - ?y:_):_ = ?z where { ?y = foreign llvm fsub(z,x) }

# Unary negation.  This operation is reversible:  you can compute the argument 
# from the value.
pub def -(x:_):_  = foreign llvm fsub(0.0,x)
pub def -(?x:_):_ = ?y where { ?x = foreign llvm fsub(0.0,y) }

# Subtraction assignment.
pub def (!x:_ -= y:_) { ?x = x - y }

# Multiplication.
pub def foreign llvm `*`=fmul(x:_, y:_):_

# Multiplication assignment.
pub def (!x:_ *= y:_) { ?x = x * y }

# Division.
pub def foreign llvm `/`=fdiv(x:_, y:_):_

# Division assignment.
pub def (!x:_ /= y:_) { ?x = x / y }

# Raise to the second power.
pub def square(x:_):_ = foreign llvm fmul(x,x)

# Absolute value.
pub def foreign c abs=`llvm.fabs.f64`(x:_):_


## Comparisons

# Equality and disequality.
pub def foreign llvm {test} `=` =fcmp_oeq(x:_, y:_)
pub def foreign llvm {test} `~=`=fcmp_one(x:_, y:_)

# Inequalities.
pub def foreign llvm {test} `<` =fcmp_olt(x:_, y:_)
pub def foreign llvm {test} `<=`=fcmp_ole(x:_, y:_)
pub def foreign llvm {test} `>` =fcmp_ogt(x:_, y:_)
pub def foreign llvm {test} `>=`=fcmp_oge(x:_, y:_)

# Three-way comparison.
pub def (x:_ <=> y:_):comparison = 
    if { x < y :: lesser | x = y :: equal | else :: greater }

# Minimum and maximum of two floats.
pub def foreign c min=`llvm.minnum.f64`(x:_, y:_):_
pub def foreign c max=`llvm.maxnum.f64`(x:_, y:_):_


## Conversion procedures

# The smallest whole number greater than or equal to the input, as a float.
pub def foreign c ceil=`llvm.ceil.f64`(x:_):_

# The largest whole number less than or equal to the input, as a float.
pub def foreign c floor=`llvm.floor.f64`(x:_):_

# The whole number closest to the input, as a float.
pub def foreign c round=`llvm.round.f64`(x:_):_

# The closest whole number to the input, as an int.
pub def iround(x:_):int = foreign llvm fptosi(round(x))

# The smallest whole number greater than or equal to the input, as an int.
pub def iceil(x:_):int = foreign llvm fptosi(ceil(x))

# The largest whole number less than or equal to the input, as an int.
pub def ifloor(x:_):int = foreign llvm fptosi(floor(x))

# Convert integers to floats
pub def foreign llvm float=sitofp(x:int):_
pub def foreign llvm float=uitofp(x:count):_


## Transcendental functions

# Square root.
pub def foreign c sqrt=`llvm.sqrt.f64`(x:_):_

# Raise `x` to the `n` power.
pub def foreign c power=`llvm.pow.f64`(x:_,n:_):_
pub def foreign c `**`=`llvm.pow.f64`(x:_, n:_):_

# Trigonometric functions.
pub def foreign c sin=`llvm.sin.f64`(x:_):_
pub def foreign c cos=`llvm.cos.f64`(x:_):_
pub def tan(x:_):_ = foreign c `llvm.sin.f64`(x) / foreign c `llvm.cos.f64`(x)
pub def foreign c asin(x:_):_
pub def foreign c acos(x:_):_
pub def foreign c atan(x:_):_
pub def foreign c atan2(x:_,y:_):_

# Convert between degrees and radians for use in trig functions
pub def from_degrees(deg:_):_   = deg*(pi/180.0)
pub def from_degrees(?deg:_):_  = ?rad where { ?deg = to_degrees(rad) }

pub def to_degrees(rad:_):_   = rad*(180.0/pi)
pub def to_degrees(?rad:_):_  = ?deg where { ?rad = from_degrees(deg) }

# Exponential:  `e` to the `x` power.
pub def foreign c exp=`llvm.exp.f64`(x:_):_

# Natural (base `e`) logarithm
pub def foreign c log=`llvm.log.f64`(x:_):_

# Common (base 10) logarithm
pub def foreign c log10=`llvm.log10.f64`(x:_):_

# Base 2 logarithm.
pub def foreign c log2=`llvm.log2.f64`(x:_):_

## Formatting 6 digits precision, trimming trailing 0s
pub def fmt(x:_):string = fmt(x, 0, ' ', 6, true)

pub def fmt(x:_, minlength:int):string = fmt(x, minlength, ' ', 6, true)

pub def fmt(x:_, minlength:int, fill:char):string =
    fmt(x, minlength, fill, 6, true)

## Formatting with explict precision
pub def fmt(x:_, minlength:int, fill:char, precision:int):string =
    fmt(x, minlength, fill, precision, false)

## Formatting with maximum precision
pub def fmt(x:_, minlength:int, fill:char, precision:int, trim:bool):string =
  s where {
    ?prefix = "" ; ?suffix = ""
    if { x < 0.0 :: ?prefix = "-" ; ?x = -x }
    if { (x >= 1e10 | x < 1e-4) ::
        ?exponent = floor(log10(x))
        !x / (10.0**exponent)
        ?suffix = "e" ,, fmt(ifloor(exponent))
    }
    ?precision_limit = 10.0 ** float(-precision)
    ?x = x + precision_limit/2.0 # round to precision
    ?base = ifloor(x)
    ?s = (fmt(base) ,, ".")
    ?x = x - float(base)
    do {
        decr(!precision)
        !x * 10.0
        ?digit = floor(x)
        !s ,, (chr(ord('0')+ifloor(digit)) | shouldnt)
        ?x = x - digit
        !precision_limit * 10.0
        # stop after 6 characters or when rest will all be 0s
        while precision > 0 & (~trim | x > precision_limit)
    }
    !s ,, suffix
    ?padding = repetition(fill, minlength-length(s)-length(prefix))
    ?s = if { fill = '0' :: prefix ,, padding ,, s
            | else       :: padding ,, prefix ,, s
    }
}

## I/O

pub def foreign c print=print_float(x:_) use !io 

pub def println(x:_) use !io { !print(x); !nl }

pub def foreign c read=read_float(?x:_) use !io 
