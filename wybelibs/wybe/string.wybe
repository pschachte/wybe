# purpose: Standard String type
# since  : 0.1

pragma no_standard_library

use wybe.c_string, wybe.range, wybe.int, wybe.char, wybe.bool, wybe.comparison
use wybe.io 

# Private constructors.
# This ensures that strings can only be manipulated with public procedures,
# providing a more typical interface
constructors empty
             # ^ An empty string
           | buffer(len:int, raw:c_string) 
             # ^ A wrapper for a c_string
             # Note: must be first non-const ctor
           | concat(left:_, right:_)
             # ^ A concatenation of two strings
           | slice(base:_, range:range)
             # ^ A slice of a string under the given range
           | singleton(c:char)
             # ^ A singleton string, a single char


## Conversion procedures.

# Convert a c_string into a string
pub def string(str:c_string):_ = s where {
    ?len = length(str)
    ?s = if { len = 0 :: empty 
            | len = 1 :: singleton(foreign lpvm access(str, 0, 1, 0)) 
            | else    :: buffer(len, str)
            }
}

# Convert a char into a string
pub def string(c:char):_ = singleton(c)

# Convert a string into a c_string
pub def c_string(s:_):c_string = str where {
    if { s = buffer(_, ?str) :: pass
       | else :: 
            ?len = length(s) + 1
            foreign lpvm alloc(len, ?str)
            foreign lpvm mutate(str, ?str, len, true, len, 0, '\0')
            ?offset = 0
            pack(s, !str, len, !offset)
    }
}

# String length
pub def length(s:_):int = 
    if { s = buffer(?len, _) :: len
       | s = concat(?left, ?right) :: length(left) + length(right)
       | s = slice(?base, ?range) :: fixed_range_size(length(base), range)
       | s = singleton(_) :: 1
       | else :: 0
       }


## Common operations

# Concatenation
pub def (x:_ ,, y:_):_ =
    if { x = empty :: y 
       | y = empty :: x
       | else :: concat(x, y)
       }

pub def (x:char ,, y:_):_ =
    if { y = empty :: singleton(x)
       | else      :: concat(singleton(x), y)
       }

pub def (x:_ ,, y:char):_ =
    if { x = empty :: singleton(y)
       | else      :: concat(x,singleton(y))
       }

# Reverse-mode cons
pub def {test} `[|]`(?head:char, ?tail:_, s:_) {
    case s in { 
        buffer(?len, ?str) :: 
            [?head | ?str] = str
            ?tail = if { len = 1 :: empty | else :: buffer(len - 1, str) }
      | concat(?left, ?right) :: 
            if { [?head | ?t] = left :: ?tail = concat(t, right)
               | else :: [?head | ?tail] = right
            }
      | slice(?base, ?range) :: 
            do { # this could be faster
                [?idx | ?range] = range
                if { ?head = base[idx] :: ?tail = slice(base, range); break }
            }
      | singleton(?head) :: ?tail = empty
      | else :: fail
    }
}

# String indexing
pub def {test} [](s:_, idx:int, ?c:char) {
    case s in { 
        buffer(?len, ?str) :: 
            (0 <= idx)
            (idx < len)
            unsafe_c_string_index(str, idx, ?c)
      | concat(?left, ?right) :: 
            ?left_len = length(left)
            if { idx < left_len :: ?c = left[idx]
               | else :: ?c = right[idx - left_len]
            }
      | slice(?base, ?range) :: ?c = base[range[idx]]
      | singleton(?c) :: (idx = 0)
      | else :: fail
    }
}

# String slicing
pub def [](s:_, r:range):_ = slice(s, r)


## Comparisons

# Equality and disequality.
pub def (x:_ =  y:_):bool = (x <=> y) =  equal
pub def (x:_ ~= y:_):bool = (x <=> y) ~= equal

# Inequalities.
pub def (x:_ <  y:_):bool = (x <=> y) <  equal
pub def (x:_ <= y:_):bool = (x <=> y) <= equal
pub def (x:_ >  y:_):bool = (x <=> y) >  equal
pub def (x:_ >= y:_):bool = (x <=> y) >= equal

# Three-way comparison.
pub def (x:_ <=> y:_):comparison = c where {
    ?c = equal
    for ?cx in x; ?cy in y {
        if { cx < cy :: ?c = lesser; break
           | cx > cy :: ?c = greater; break 
        }
    }
    if { c = equal :: 
        ?lx = length(x)
        ?ly = length(y)
        if { lx < ly :: ?c = lesser | lx > ly :: ?c = greater } 
    }
}

## Formatting
pub def fmt(x:_):_ = x


## I/O

pub def print(x:_) use !io {
    case x in { 
        buffer(_, ?str) :: !print(str)
      | concat(?left, ?right) :: !print(left); !print(right)
      | slice(_, _) :: for ?c in x { !print(c) }
      | singleton(?c) :: !print(c)
    }
}

pub def println(x:_) use !io { !print(x); !nl }

pub def read(?x:_) use !io { !read(?str:c_string); ?x = string(str) }


## Private helper procedures

# Pack a string into a c_string, from offset
# Note: mutates raw, increasing offset
def pack(s:_, !raw:c_string, size:int, !offset:int) {
    case s in {
        buffer(_, ?str) :: 
            for ?c in str {
                foreign lpvm mutate(raw, ?raw, offset, true, size, 0, c)
                incr(!offset)
            }
      | concat(?left, ?right) :: 
            pack(left, !raw, size, !offset)
            pack(right, !raw, size, !offset)
            incr(!offset)
      | slice(_, _) :: 
            for ?c in s {
                foreign lpvm mutate(raw, ?raw, offset, true, size, 0, c)
                incr(!offset)
            }
      | singleton(?c) ::
            foreign lpvm mutate(raw, ?raw, offset, true, size, 0, c)
            incr(!offset)
    }
}

# Fix a range's sie for the string
# Note: there is probably a cleaner way to do this
def fixed_range_size(hi:int, r:range, ?sz:int) {
    r = range(?start, ?stride, ?end)
    if { 0 <= stride :: 
            ?lo = max(start, start + -start / stride * stride); 
            ?hi = min(hi, end)
       | else :: 
            ?lo = max(-1, end); 
            ?hi = min(start, hi + stride + (start - hi - 1) % -stride + 1) 
            ?stride = -stride  
       }
    ?sz = if { hi <= lo :: 0 | else :: (hi - lo - 1) / stride + 1 }
}
