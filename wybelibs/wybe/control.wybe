# purpose: Standard Control Library
# since  : 0.1

pragma no_standard_library  # Standard library can't depend on itself!

use wybe.int, wybe.bool, wybe.c_string, wybe.string

# Exit right away with the specified code (0 = successful termination).

pub def {terminal,semipure} exit(code:int) {
    foreign c {terminal,semipure} exit(code)
}


# Print the source location and and error message, and abort the computation.

pub def {terminal,semipure} error(message:string) use call_source_location {
    foreign c {terminal,semipure} error_exit(call_source_location, c_string(message))
}

pub def {terminal,pure} error(message:string, ?result:T) use call_source_location {
    !error(message)
}

pub def {terminal,semipure} error(message:c_string) use call_source_location {
    foreign c {terminal,semipure} error_exit(call_source_location, message)
}


pub def {terminal,semipure} shouldnt use call_source_location {
    !error("shouldn't happen!")
}

pub def {terminal,pure} shouldnt(?result:T) use call_source_location {
    !error("shouldn't happen!")
}

pub def {semipure} assert(condition:bool) use call_source_location {
    if { ~condition :: !error(c"assertion failed") }
}
